resources_dir = read("prop:resources_dir")

open class Resource {
      category: String
      id: String
      description: String
      architecture: String
      size: Int
      tags: Listing<String>
      is_zipped: Boolean = false
      md5sum: String
      source: String
      url: String
      license: String = "https://www.spec.org/cpu2017/Docs/licenses.html"
      author: Listing<String> = new Listing {
              "Various"
      }
      gem5_versions: Listing<String> = new Listing {"24.0" "24.1" "25.0"}
      source_url: String
      resource_version: String = "1.0.0"
      example_usage: String
}

class DiskImageResource extends Resource {
      category: String = "disk-image"
      root_partition: String = "1"
}

class BinaryResource extends Resource {
      category: String = "binary"
}

class Params {
      arguments: Listing<String>
}

class ShortResource {
      id: String
      resource_version: String
}

class Workload {
      category: String = "workload"
      id: String
      description: String
      `function`: String
      resources: Mapping<String, ShortResource>
      architecture: String
      tags: Listing<String>
      license: String = "https://www.spec.org/cpu2017/Docs/licenses.html"
      author: Listing<String> = new Listing {
          "Various"
      }
      additional_params: Params
      source_url: String
      resource_version: String = "1.0.0"
      gem5_versions: Listing<String> = new Listing {"24.0" "24.1" "25.0"}
      example_usage: String
}

class ShortWorkload {
      id: String
      resource_version: String
      input_group: Listing<String>
}

class Suite {
      id: String
      description: String
      category: String = "suite"
      resource_version: String = "1.0.0"
      workloads: Listing<ShortWorkload>
      gem5_versions: Listing<String> = new Listing {"24.0" "24.1" "25.0"}
      example_usage: String
      tags: Listing<String>
      author: Listing<String> = new Listing {
          "Various"
      }
      architecture: String = "RISCV"
      license: String = "https://www.spec.org/cpu2017/Docs/licenses.html"
      additional_params: Mapping<String, String>?
      source_url: String = ""
}

hidden binaries = new Mapping {
  ["gcc-lbm"] {
    name = "gcc-lbm"
    binary = "cpugcc_r_base"
    md5sum = "476c03e9c3c937f29c1cd5025728bdfb"
    arguments = new Listing {
      "\(resources_dir)/benchmarks/inputs/gcc/lbm.c"
      "-o"
      "gcc_tmp.o"
    }
  }
  ["gcc-mcf"] {
    name = "gcc-mcf"
    binary = "cpugcc_r_base"
    md5sum = "476c03e9c3c937f29c1cd5025728bdfb"
    arguments = new Listing {
      "\(resources_dir)/benchmarks/inputs/gcc/mcf.c"
      "-o"
      "gcc_tmp.o"
    }
  }
  ["x264-scalar"] {
    name = "x264-scalar"
    binary = "x264_r.s"
    md5sum = "40f889e2649f1156569a9483281fcb2c"
    arguments = new Listing {
      "-p 1"
      "-B 1000"
      "-o bb.mkv"
      "\(resources_dir)/benchmarks/inputs/x264/BuckBunny.264"
      "854x480"
    }
  }
  ["x264-vector"] {
    name = "x264-vector"
    binary = "x264_r.v"
    md5sum = "c365d2a47726851f39c42229902beb01"
    arguments = new Listing {
      "-p 1"
      "-B 1000"
      "-o bb.mkv"
      "\(resources_dir)/benchmarks/inputs/x264/BuckBunny.264"
      "854x480"
    }
  }
  ["lbm"] {
    name = "lbm"
    binary = "lbm_r_base"
    md5sum = "fe4f19b4c4ae13b367b489aaecb8c1b2"
    arguments = new Listing {
      "20"
      "reference.dat"
      "0"
      "1"
      "\(resources_dir)/benchmarks/inputs/lbm/100_100_130_cf_a.of"
    }
  }
  ["mcf"] {
    name = "mcf"
    binary = "mcf_r_base"
    md5sum = "62b032e61433fbeef498b61753fdd5a6"
    arguments = new Listing {
      "\(resources_dir)/benchmarks/inputs/mcf/mcf1.in"
    }
  }
  ["conv1-scalar"] {
    name = "conv1-scalar"
    binary = "conv1"
    md5sum = "011c068db27bb24b4593f15649c798c5"
    arguments = new Listing {}
  }
  ["conv1-vector"] {
    name = "conv1-vector"
    binary = "conv1_vec"
    md5sum = "de6cb6ea33b98e4f7d559be367549f99"
    arguments = new Listing {}
  }
  ["conv2-scalar"] {
    name = "conv2-scalar"
    binary = "conv2"
    md5sum = "6f286db4e7024b99b69e8b39168ba92d"
    arguments = new Listing {}
  }
  ["conv2-vector"] {
    name = "conv2-vector"
    binary = "conv2_vect"
    md5sum = "c3b8c4eb05950f59d2258539a43c3b07"
    arguments = new Listing {}
  }
}

hidden a5_binaries = new Mapping {
  ["scalar-naive-matmul"] {
    name = "scalar-naive-matmul"
    binary = "riscv-scalar-naive-matmul"
    md5sum = "27bc225996eaa1bb4e19752e58ca31e5"
  }
  ["scalar-interchange-matmul"] {
    name = "scalar-interchange-matmul"
    binary = "riscv-scalar-interchange-matmul"
    md5sum = "c6f2f28404ac08cfdac6f9286e61add5"
  }
  ["scalar-interchange-blocking-matmul"] {
    name = "scalar-interchange-blocking-matmul"
    binary = "riscv-scalar-interchange-blocking-matmul"
    md5sum = "4964ca127e9f36422ef3b32b69d80b3f"
  }
  ["vector-naive-matmul"] {
    name = "vector-naive-matmul"
    binary = "riscv-vectorized-naive-matmul"
    md5sum = "d66e48ba80c0009e5d9050d4deb80565"
  }
  ["vector-interchange-matmul"] {
    name = "vector-interchange-matmul"
    binary = "riscv-vectorized-interchange-matmul"
    md5sum = "2e071a65a2b32afddb75ef74e03b04e7"
  }
  ["vector-interchange-blocking-matmul"] {
    name = "vector-interchange-blocking-matmul"
    binary = "riscv-vectorized-interchange-blocking-matmul"
    md5sum = "60352dfa011fd6fa187c92af9cbc40c9"
  }
}


resources = new Listing {
  for (_binary in binaries) {
    (Resource) {
      category = "binary"
      id = "riscv-spec-\(_binary.name)-bin"
      description = "Benchmark \(_binary.name) compiled for RISC-V."
      architecture = "RISCV"
      size = 1
      tags = new Listing {"spec"}
      md5sum = "\(_binary.md5sum)"
      source = "429bin/\(_binary.binary).riscv"
      url = "file://\(resources_dir)/benchmarks/429bin/\(_binary.binary).riscv"
      source_url = ""
      example_usage = "obtain_resource(\"riscv-spec-\(_binary.name)-bin\")"
    }

    (Workload) {
      category = "workload"
      id = "riscv-spec-\(_binary.name)-run-se"
      description = "spec benchmark \(_binary.name) run configuration for RISC-V SE model."
      `function` = "set_se_binary_workload"
      resources {
        ["binary"] = (ShortResource) {
          id = "riscv-spec-\(_binary.name)-bin"
          resource_version = "1.0.0"
        }
      }
      additional_params {
        arguments = _binary.arguments
      }
      architecture = "RISCV"
      tags = new Listing {"spec" "se" "riscv"}
      source_url = ""
      example_usage = "obtain_resource(\"riscv-spec-\(_binary.name)-run-se\")"
    }
  }

  for (_binary in a5_binaries) {
    (BinaryResource) {
      id = "\(_binary.name)-bin"
      description = "A statically compiled matmul kernel in riscv"
      architecture = "RISCV"
      size = 1
      tags = new Listing {"a5"}
      md5sum = "\(_binary.md5sum)"
      source = "\(resources_dir)/sources/a5/cpu/matmul.c"
      url = "file://\(resources_dir)/429bin/\(_binary.name).riscv"
      source_url = ""
      example_usage = "obtain_resource(\"a5/\(_binary.name)-bin\")"
    }

    (Workload) {
      id = "\(_binary.name)-wkld"
      description = "A5 benchmark run configuration for RISC-V."
      `function` = "set_a5_binary_workload"
      resources {
        ["binary"] = (ShortResource) {
          id = "\(_binary.name)-bin"
          resource_version = "1.0.0"
        }
      }
      architecture = "RISCV"
      tags = new Listing {"a5" "riscv"}
      source_url = ""
      example_usage = "obtain_resource(\"a5/\(_binary.name)-run\")"
    }
  }

  (Suite) {
    id = "assignment5-vector-alu-suite"
    description = "Suite for running assignment 5 vectorized matmul kernels"
    architecture = "RISCV"
    workloads = new Listing {
      for (_binary in a5_binaries) {
        (ShortWorkload) {
          id = "\(_binary.name)-wkld"
          resource_version = "1.0.0"
          input_group = new Listing { "vALU" }
        }
      }     
    }
    tags = new Listing {"a5" "riscv"}
    author = new Listing {"Ayrton Chilibeck"}
    license = "WTFPL"
    example_usage = "obtain_resource(\"assignment5-vector-alu-suite\")"
  }

  (DiskImageResource) {
    id = "gpufs-disk-image"
    description = "Disk image for GPUFS"
    architecture = "X86"
    size = 58720256000
    tags = new Listing {"gpufs" "large"}
    md5sum = "39b2d9c21509fc0cf48d860bb2bd5f58"
    source = "https://github.com/gem5/gem5-resources"
    url = "file:///home/stormblessed/Code/Architecture/gem5-resources/src/x86-ubuntu-gpu-ml/disk-image/x86-ubuntu-gpu-ml"
    source_url = ""
    example_usage = "obtain_resource(\"gpufs-disk-image\")"
  }
  (Resource) {
    category = "kernel"
    id = "gpufs-kernel"
    description = "Kernel for GPUFS"
    architecture = "X86"
    size = 1
    tags = new Listing {"gpufs"}
    md5sum = "c3b9ef3102b5f73dcabb025c2d98105b"
    source = ""
    url = "file://\(resources_dir)/benchmarks/429bin/gpufs-kernel"
    source_url = "https://github.com/gem5/gem5-resources"
    example_usage = "obtain_resource(\"gpufs-kernel\")"
  }
  (Resource) {
    category = "binary"
    id = "gpufs-square"
    description = "Simple program that squares a vector"
    architecture = "X86"
    size = 38688
    tags = new Listing {"gpufs"}
    md5sum = "dd12fe4e37b432e34af36e2c94f2e86a"
    source = "https://github.com/gem5/gem5-resources"
    source_url = ""
    url = "file://\(resources_dir)/benchmarks/429bin/square.default"
    example_usage = "obtain_resource(\"gpufs-square\")"
  }
}

output {
  renderer = new JsonRenderer{}
  value = resources
}
